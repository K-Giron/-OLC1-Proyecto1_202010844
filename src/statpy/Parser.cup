
// ------------  Paquete e importaciones ------------
package statpy;

import java_cup.runtime.*;


//------> Codigo para el parser
//------> Declaracion de variables, funciones y funciones de error

parser code 
{:
    public static String resultado = ""; 

    public void syntax_error(Symbol s)
    {
        System.err.println("Error Sintactico: "+ s.value + " - Fila: " + s.right + " - Columna: " + s.left + ". Recuperado" );
        data.Info.listaErrores.add(new data.Error("Error Sintactico", "No reconocido el caracter: "+ s.value,
                s.right, s.left));
    }

    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception
    {
        System.err.println("Error Sintactico: "+ s.value + " - Fila: " + s.right + " - Columna: " + s.left + ". Sin recuperacion." );
        data.Info.listaErrores.add(new data.Error("Error Sintactico", "No reconocido el caracter: "+ s.value,
                s.right, s.left));
}

:}


//------> Codigo para las acciones gramaticales (no tocar)
action code
{:  :}


//------> Declaración de terminales
terminal String ENTERO,ID,DECIMAL;
terminal String AND,OR,NOT;
terminal String POR, MAS, IGUAL,DIVISION,MENOS,TRUE,FALSE;
terminal String MAYOR, MENOR, MAYORIGUAL,MENORIGUAL,IGUALIGUAL,DISTINTO;
terminal String PARENTESIS_A, PARENTESIS_C,TEXTOENTRECOMILLAS,LLAVE_A,LLAVE_C;
terminal String PUNTOYCOMA, PUNTO,APOSTROFE;
terminal String CONSOLE,WRITE;
terminal String IF,ELSE,WHILE;
terminal String INT,DOUBLE,CHAR,BOOL,STRING;

//------> Declaración de no terminales
non terminal inicio;
non terminal asignacion, impresion,tipo_if,tipo_while;
non terminal lista_instr,instr_if,instr_while;
non terminal instruccion;
non terminal expresion;

//------> precedencia
precedence left AND,OR;
precedence left IGUALIGUAL,DISTINTO;
precedence left MAYOR,MENOR,MAYORIGUAL,MENORIGUAL;
precedence left MAS,MENOS;
precedence left POR,DIVISION;


//------> Definir Simbolo Inicial
start with inicio; 


// ------------>  Producciones  <------------

inicio ::= lista_instr
;

lista_instr ::= lista_instr instruccion
        | instruccion
;

instruccion::= tipo_if:val                                                       {:  :}
    | tipo_while                                                                        {: :}
    | asignacion:val PUNTOYCOMA                          {: statpy.Parser.resultado= statpy.Parser.resultado+val.toString()+" \n" ;:}
    | impresion:val PUNTOYCOMA                      {: statpy.Parser.resultado= statpy.Parser.resultado+val.toString()+" \n" ; :}
    | error PUNTOYCOMA
;

impresion ::= CONSOLE PUNTO WRITE PARENTESIS_A expresion:val PARENTESIS_C  
                        {: RESULT= "print("+ val.toString()+")";  :}
;
tipo_if::= instr_if lista_instr LLAVE_C                               
;
instr_if ::= IF:val PARENTESIS_A expresion:var PARENTESIS_C LLAVE_A
                            {: statpy.Parser.resultado += val.toString()+" " + var.toString() + ":\n"; :}  
                | ELSE:val LLAVE_A
                            {: statpy.Parser.resultado += val.toString()+ ":\n"; :}  
                | ELSE IF:val PARENTESIS_A expresion:var PARENTESIS_C LLAVE_A
                            {: statpy.Parser.resultado += "elif "+var.toString()+ ":\n"; :}  
;
tipo_while::= instr_while lista_instr LLAVE_C                               
; 
instr_while ::= WHILE:val PARENTESIS_A expresion:var PARENTESIS_C LLAVE_A 
                            {: statpy.Parser.resultado += val.toString()+" " + var.toString() + ":\n"; :}
;

asignacion ::= INT ID:nombre IGUAL:signo expresion:var           {: RESULT= nombre.toString()+" "+signo.toString()+" "+var.toString(); :}
                    | DOUBLE ID:nombre IGUAL:signo expresion:var           {: RESULT= nombre.toString()+" "+signo.toString()+" "+var.toString(); :}
                    | CHAR ID:nombre IGUAL:signo expresion:var           {: RESULT= nombre.toString()+" "+signo.toString()+" "+var.toString(); :}
                    | BOOL ID:nombre IGUAL:signo expresion:var           {: RESULT= nombre.toString()+" "+signo.toString()+" "+var.toString(); :}
                    | STRING ID:nombre IGUAL:signo expresion:var           {: RESULT= nombre.toString()+" "+signo.toString()+" "+var.toString(); :}
                    
;


expresion::= expresion:a MAS expresion:b                {: RESULT=a.toString()+" + "+b.toString(); :}
        | expresion:a POR expresion:b                {: RESULT=a.toString()+" * "+b.toString(); :}
        | expresion:a DIVISION expresion:b                {: RESULT=a.toString()+" / "+b.toString(); :}
        | expresion:a MENOS expresion:b                {: RESULT=a.toString()+" - "+b.toString(); :}
        | expresion:a MAYOR expresion:b                {: RESULT=a.toString()+" > "+b.toString(); :}
        | expresion:a MENOR expresion:b                {: RESULT=a.toString()+" < "+b.toString(); :}
        | expresion:a MAYORIGUAL expresion:b                {: RESULT=a.toString()+" >= "+b.toString(); :}
        | expresion:a MENORIGUAL expresion:b                {: RESULT=a.toString()+" <= "+b.toString(); :}
        | expresion:a IGUALIGUAL expresion:b                {: RESULT=a.toString()+" == "+b.toString(); :}
        | expresion:a DISTINTO expresion:b                {: RESULT=a.toString()+" != "+b.toString(); :}
        | NOT:val expresion:a                                     {: RESULT = " not "+a.toString(); :}
        | expresion:a AND expresion:b                {: RESULT=a.toString()+" and "+b.toString(); :}
        | expresion:a OR expresion:b                {: RESULT=a.toString()+" or "+b.toString(); :}
        | TRUE:val                                 {: RESULT =val; :}
        | FALSE:val                  {: RESULT = val; :}
        | ENTERO:val                                    {: RESULT = val; :}
        | DECIMAL:val                                    {: RESULT = val; :}
        | ID:val                                        {: RESULT = val; :}
        | TEXTOENTRECOMILLAS:val       {: RESULT = val; :}
        | TEXTOENTRECOMILLAS:val MAS expresion:a       {: RESULT =val.toString()+" , "+a.toString(); :}
        
        
;
